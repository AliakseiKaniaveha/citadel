/*
 * For more details take a look at the 'Building Java & JVM projects' chapter in the Gradle
 * User Manual available at https://docs.gradle.org/7.2/userguide/building_java_projects.html
 */

plugins {
    //Apply the application plugin to build and run Java app
    id 'application'

    //Spring Boot
    //doesn't applied to all configurations as we need it for implementation only
    id 'org.springframework.boot' version '2.6.4' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    //Ide IDE perks for Gradle
    id 'idea'
}

dependencyManagement {
    implementation {
        imports {
            //the org.springframework.boot BOM
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        }
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}


sourceSets {
    /*main {
        //application sources
        //it's default for gradle so no need to define explicitly
        java {
            srcDirs = ['src/main/java']
        }
    }*/
    /*test {
        //main's unit tests
        //it's default for gradle so no need to define explicitly
        java {
            srcDirs = ['src/test/java']
        }
    }*/
    uiTest {
        java {
            srcDirs = ['src/test-ui/java']
        }
    }
}

idea {
    module {
        testSourceDirs += sourceSets.uiTest.java.srcDirs
    }
}

dependencies {


    /*******************************************************************************************************************
     *                                                Application                                                      *
     ******************************************************************************************************************/
    //Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    /******************************************************************************************************************/


    /*******************************************************************************************************************
     *                                                Unit Tests                                                       *
     ******************************************************************************************************************/
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'  //junit5
    testImplementation 'org.hamcrest:hamcrest:2.2'  //powerful assertions
    /******************************************************************************************************************/


    /*******************************************************************************************************************
     *                                                Integration Tests                                                *
     ******************************************************************************************************************/
    testImplementation 'org.springframework.boot:spring-boot-starter-test'  //Spring Context in tests
    /******************************************************************************************************************/


    /*******************************************************************************************************************
     *                                                Functional Tests                                                 *
     ******************************************************************************************************************/
    //tbd
    /******************************************************************************************************************/


    /*******************************************************************************************************************
     *                                                UI Tests                                                         *
     ******************************************************************************************************************/
    uiTestImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'    //junit5
    //Selenide
    uiTestImplementation 'com.codeborne:selenide:6.3.4'
    /******************************************************************************************************************/
}

application {
    // Define the main class for the application.
    mainClass = 'kaniaveha.aliaksei.citadel.App'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

tasks.register('uiTest', Test) {
    description = 'Runs UI tests.'
    group = 'verification'

    testClassesDirs = sourceSets.uiTest.output.classesDirs
    classpath = sourceSets.uiTest.runtimeClasspath

    useJUnitPlatform()

    //propagate system properties so args of the form -D<key>=<val> can be used in tests
    systemProperties(System.getProperties())
}